# Set application version:
file(READ ${CMAKE_CURRENT_LIST_DIR}/../version.txt OPENTODOLIST_VERSION)
string(STRIP "${OPENTODOLIST_VERSION}" OPENTODOLIST_VERSION)

if("${OPENTODOLIST_VERSION}" STREQUAL "")
    set(OPENTODOLIST_VERSION_MAJOR 0)
    set(OPENTODOLIST_VERSION_MINOR 0)
    set(OPENTODOLIST_VERSION_PATCH 0)
else()
    string(REPLACE "." ";" OPENTODOLIST_VERSION_LIST "${OPENTODOLIST_VERSION}")
    list(GET OPENTODOLIST_VERSION_LIST 0 OPENTODOLIST_VERSION_MAJOR)
    list(GET OPENTODOLIST_VERSION_LIST 1 OPENTODOLIST_VERSION_MINOR)
    list(GET OPENTODOLIST_VERSION_LIST 2 OPENTODOLIST_VERSION_PATCH)
endif()

add_definitions(-DOPENTODOLIST_VERSION="${OPENTODOLIST_VERSION}")


find_program(GIT_EXECUTABLE git)
if(GIT_EXECUTABLE)
    execute_process(
        COMMAND
            ${GIT_EXECUTABLE} rev-parse HEAD
        OUTPUT_VARIABLE
            OPENTODOLIST_GIT_REVISION
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    add_definitions(-DOPENTODOLIST_GIT_REVISION="${OPENTODOLIST_GIT_REVISION}")
    execute_process(
        COMMAND
            ${GIT_EXECUTABLE} describe --tags
        OUTPUT_VARIABLE
            OPENTODOLIST_GIT_VERSION
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    add_definitions(-DOPENTODOLIST_GIT_VERSION="${OPENTODOLIST_GIT_VERSION}")
endif()

if(NOT OPENTODOLIST_ANDROID_VERSION_CODE)
    set(OPENTODOLIST_ANDROID_VERSION_CODE 0)
endif()
