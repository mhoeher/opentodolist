top_srcdir=$$PWD
top_builddir=$$shadowed($$PWD)

isEmpty(INSTALL_PREFIX) {
    INSTALL_PREFIX = /usr/local
}

INSTALL_SUFFIX_BIN = /bin
win32 {
    INSTALL_SUFFIX_LIB = /bin
} else {
    INSTALL_SUFFIX_LIB = /lib
}

# Use built-in LMDB by default:
CONFIG += qlmdb_with_builtin_lmdb synqclient_with_no_tests

# On macOS, use static qlmdb library:
macos:CONFIG+=qlmdb_with_static_libs synqclient_with_static_libs

exists($$PWD/.flatpak-build):CONFIG *= opentodolist_flatpak
opentodolist_flatpak {
    # Load Qt modules located in custom location:
    mods = $$files(/app/usr/lib/mkspecs/modules/qt_*.pri)
    for (mod, mods) {
        QT_MODULE_INCLUDE_BASE = /app$$[QT_INSTALL_HEADERS]
        QT_MODULE_LIB_BASE = /app$$[QT_INSTALL_LIBS]
        QT_MODULE_HOST_LIB_BASE = /app$$[QT_HOST_LIBS]
        QT_MODULE_BIN_BASE = /app$$[QT_INSTALL_BINS]
        include($$mod)
    }
    unset(mods)
    QT_MODULES = $$unique(QT_MODULES)   # In case modules appear in multiple places
    unset(QT_MODULE_INCLUDE_BASE)
    unset(QT_MODULE_LIB_BASE)
    unset(QT_MODULE_HOST_LIB_BASE)
    unset(QT_MODULE_BIN_BASE)
}
